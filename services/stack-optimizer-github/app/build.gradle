apply plugin: 'JavaRestApiGenerator'
apply plugin: 'GCRPublish'
apply plugin: "com.diffplug.spotless"
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'BlumeSpringVersion'

task buildJavascriptClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
    generatorName = "javascript"
    inputSpec="$buildDir/swagger.json"
    validateSpec=false
    additionalProperties = [
            projectName: "apss_api",
            projectVersion: "1.0.0",
            npmRepository: "https://nexus.rez1.com/repository/npm-private/",
            usePromises: true
    ]
    outputDir="${project.rootProject.projectDir}/rest-sdk-js"
}

springBoot {
    mainClass = 'com.blumeglobal.stackoptimizer.StackOptimizerApplication'
    buildInfo()
}

apply plugin: 'BlumeSwagger'
javaRestApi {
    swaggerLocation="${buildDir}/swagger.json"
    outputSrcDir=file("${project.rootProject.projectDir}/rest-sdk-java/src/gen/java")
    modelPackage='com.blumeglobal.stackoptimizer.rest.model'
    apiPackage='com.blumeglobal.stackoptimizer.rest.api'
}

// to skip test cases, uncomment next line
// test.onlyIf {false}

jar {
    enabled = true
    manifest {
        attributes 'Main-Class': 'com.blumeglobal.stackoptimizer.StackOptimizerApplication'
    }
}

dependencies {
    api project(":model")
    api project(":jpamodel")
    api "com.blumeglobal:core-mysql:${coreArchitectureVersion}"
    api "com.blumeglobal:core-security:${coreArchitectureVersion}"
    api "com.blumeglobal:core-web:${coreArchitectureVersion}"
    api "com.blumeglobal:core-mongo:${coreArchitectureVersion}"
    api "com.blumeglobal:core-swagger:${coreArchitectureVersion}"


    testImplementation "com.blumeglobal:core-test:${coreArchitectureVersion}"
    testImplementation "com.blumeglobal:core-mysql-test:${coreArchitectureVersion}"
    testImplementation "com.blumeglobal:core-mongo-test:${coreArchitectureVersion}"

    api group: 'org.slf4j', name: 'slf4j-ext', version: '1.7.5'

    annotationProcessor "com.querydsl:querydsl-apt:4.2.1:jpa",
            "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final",
            "javax.annotation:javax.annotation-api:1.3.2",
            "org.projectlombok:lombok:1.18.20"

    bootJar {
        enabled = true
    }
}

task runWithExec(type: Exec) {
    dependsOn assemble
    group = "Execution"
    description = "Run the main class with ExecTask"
    commandLine "java", "-classpath", sourceSets.main.runtimeClasspath.getAsPath(), 'com.blumeglobal.stackoptimizer.StackOptimizerApplication'
}

task runWithJavaExec(type: JavaExec) {
    group = "Execution"
    description = "Run the main class with JavaExecTask"
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.blumeglobal.stackoptimizer.StackOptimizerApplication'
}


spotless {
    java {
        importOrder()
        removeUnusedImports()
        eclipse().configFile('tools/micro-service-formatter.xml')
        licenseHeaderFile('tools/blume-license-header-file.txt')
    }
}

test {
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                minimum = 0.5
            }
            excludes = ['**.*metrics*.*',
                        '**.*constant*.*',
                        '**.*dto*.*',
                        '**.*External*.*',
                        '**.*Test*.*'
            ]
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacoco/report')
    }
}